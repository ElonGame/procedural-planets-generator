#version 430
[ComputeShader]









// PERLIN NOISE

// taken from: https://github.com/ashima/webgl-noise/blob/master/src/noise3D.glsl

//
// Description : Array and textureless GLSL 2D/3D/4D simplex 
//               noise functions.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : stegu
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//               https://github.com/stegu/webgl-noise
// 

vec3 perlinNoise_mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 perlinNoise_mod289(vec4 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 perlinNoise_permute(vec4 x) {
     return perlinNoise_mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v)
  { 
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

// First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

// Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
  //   x1 = x0 - i1  + 1.0 * C.xxx;
  //   x2 = x0 - i2  + 2.0 * C.xxx;
  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

// Permutations
  i = perlinNoise_mod289(i); 
  vec4 p = perlinNoise_permute( perlinNoise_permute( perlinNoise_permute( 
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

// Gradients: 7x7 points over a square, mapped onto an octahedron.
// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
  float n_ = 0.142857142857; // 1.0/7.0
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

//Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

// Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                dot(p2,x2), dot(p3,x3) ) );
  }

// PERLIN NOISE






// WORLEY NOISE

// taken from https://github.com/Erkaman/glsl-worley/blob/master/worley3D.glsl

// Permutation polynomial: (34x^2 + x) mod 289
vec3 worleyNoise_permute(vec3 x) {
  return mod((34.0 * x + 1.0) * x, 289.0);
}

vec3 worleyNoise_dist(vec3 x, vec3 y, vec3 z,  bool manhattanDistance) {
  return manhattanDistance ?  abs(x) + abs(y) + abs(z) :  (x * x + y * y + z * z);
}

vec2 worley(vec3 P, float jitter, bool manhattanDistance) {
float K = 0.142857142857; // 1/7
float Ko = 0.428571428571; // 1/2-K/2
float  K2 = 0.020408163265306; // 1/(7*7)
float Kz = 0.166666666667; // 1/6
float Kzo = 0.416666666667; // 1/2-1/6*2

	vec3 Pi = mod(floor(P), 289.0);
 	vec3 Pf = fract(P) - 0.5;

	vec3 Pfx = Pf.x + vec3(1.0, 0.0, -1.0);
	vec3 Pfy = Pf.y + vec3(1.0, 0.0, -1.0);
	vec3 Pfz = Pf.z + vec3(1.0, 0.0, -1.0);

	vec3 p = worleyNoise_permute(Pi.x + vec3(-1.0, 0.0, 1.0));
	vec3 p1 = worleyNoise_permute(p + Pi.y - 1.0);
	vec3 p2 = worleyNoise_permute(p + Pi.y);
	vec3 p3 = worleyNoise_permute(p + Pi.y + 1.0);

	vec3 p11 = worleyNoise_permute(p1 + Pi.z - 1.0);
	vec3 p12 = worleyNoise_permute(p1 + Pi.z);
	vec3 p13 = worleyNoise_permute(p1 + Pi.z + 1.0);

	vec3 p21 = worleyNoise_permute(p2 + Pi.z - 1.0);
	vec3 p22 = worleyNoise_permute(p2 + Pi.z);
	vec3 p23 = worleyNoise_permute(p2 + Pi.z + 1.0);

	vec3 p31 = worleyNoise_permute(p3 + Pi.z - 1.0);
	vec3 p32 = worleyNoise_permute(p3 + Pi.z);
	vec3 p33 = worleyNoise_permute(p3 + Pi.z + 1.0);

	vec3 ox11 = fract(p11*K) - Ko;
	vec3 oy11 = mod(floor(p11*K), 7.0)*K - Ko;
	vec3 oz11 = floor(p11*K2)*Kz - Kzo; // p11 < 289 guaranteed

	vec3 ox12 = fract(p12*K) - Ko;
	vec3 oy12 = mod(floor(p12*K), 7.0)*K - Ko;
	vec3 oz12 = floor(p12*K2)*Kz - Kzo;

	vec3 ox13 = fract(p13*K) - Ko;
	vec3 oy13 = mod(floor(p13*K), 7.0)*K - Ko;
	vec3 oz13 = floor(p13*K2)*Kz - Kzo;

	vec3 ox21 = fract(p21*K) - Ko;
	vec3 oy21 = mod(floor(p21*K), 7.0)*K - Ko;
	vec3 oz21 = floor(p21*K2)*Kz - Kzo;

	vec3 ox22 = fract(p22*K) - Ko;
	vec3 oy22 = mod(floor(p22*K), 7.0)*K - Ko;
	vec3 oz22 = floor(p22*K2)*Kz - Kzo;

	vec3 ox23 = fract(p23*K) - Ko;
	vec3 oy23 = mod(floor(p23*K), 7.0)*K - Ko;
	vec3 oz23 = floor(p23*K2)*Kz - Kzo;

	vec3 ox31 = fract(p31*K) - Ko;
	vec3 oy31 = mod(floor(p31*K), 7.0)*K - Ko;
	vec3 oz31 = floor(p31*K2)*Kz - Kzo;

	vec3 ox32 = fract(p32*K) - Ko;
	vec3 oy32 = mod(floor(p32*K), 7.0)*K - Ko;
	vec3 oz32 = floor(p32*K2)*Kz - Kzo;

	vec3 ox33 = fract(p33*K) - Ko;
	vec3 oy33 = mod(floor(p33*K), 7.0)*K - Ko;
	vec3 oz33 = floor(p33*K2)*Kz - Kzo;

	vec3 dx11 = Pfx + jitter*ox11;
	vec3 dy11 = Pfy.x + jitter*oy11;
	vec3 dz11 = Pfz.x + jitter*oz11;

	vec3 dx12 = Pfx + jitter*ox12;
	vec3 dy12 = Pfy.x + jitter*oy12;
	vec3 dz12 = Pfz.y + jitter*oz12;

	vec3 dx13 = Pfx + jitter*ox13;
	vec3 dy13 = Pfy.x + jitter*oy13;
	vec3 dz13 = Pfz.z + jitter*oz13;

	vec3 dx21 = Pfx + jitter*ox21;
	vec3 dy21 = Pfy.y + jitter*oy21;
	vec3 dz21 = Pfz.x + jitter*oz21;

	vec3 dx22 = Pfx + jitter*ox22;
	vec3 dy22 = Pfy.y + jitter*oy22;
	vec3 dz22 = Pfz.y + jitter*oz22;

	vec3 dx23 = Pfx + jitter*ox23;
	vec3 dy23 = Pfy.y + jitter*oy23;
	vec3 dz23 = Pfz.z + jitter*oz23;

	vec3 dx31 = Pfx + jitter*ox31;
	vec3 dy31 = Pfy.z + jitter*oy31;
	vec3 dz31 = Pfz.x + jitter*oz31;

	vec3 dx32 = Pfx + jitter*ox32;
	vec3 dy32 = Pfy.z + jitter*oy32;
	vec3 dz32 = Pfz.y + jitter*oz32;

	vec3 dx33 = Pfx + jitter*ox33;
	vec3 dy33 = Pfy.z + jitter*oy33;
	vec3 dz33 = Pfz.z + jitter*oz33;

	vec3 d11 = worleyNoise_dist(dx11, dy11, dz11, manhattanDistance);
	vec3 d12 =worleyNoise_dist(dx12, dy12, dz12, manhattanDistance);
	vec3 d13 = worleyNoise_dist(dx13, dy13, dz13, manhattanDistance);
	vec3 d21 = worleyNoise_dist(dx21, dy21, dz21, manhattanDistance);
	vec3 d22 = worleyNoise_dist(dx22, dy22, dz22, manhattanDistance);
	vec3 d23 = worleyNoise_dist(dx23, dy23, dz23, manhattanDistance);
	vec3 d31 = worleyNoise_dist(dx31, dy31, dz31, manhattanDistance);
	vec3 d32 = worleyNoise_dist(dx32, dy32, dz32, manhattanDistance);
	vec3 d33 = worleyNoise_dist(dx33, dy33, dz33, manhattanDistance);

	vec3 d1a = min(d11, d12);
	d12 = max(d11, d12);
	d11 = min(d1a, d13); // Smallest now not in d12 or d13
	d13 = max(d1a, d13);
	d12 = min(d12, d13); // 2nd smallest now not in d13
	vec3 d2a = min(d21, d22);
	d22 = max(d21, d22);
	d21 = min(d2a, d23); // Smallest now not in d22 or d23
	d23 = max(d2a, d23);
	d22 = min(d22, d23); // 2nd smallest now not in d23
	vec3 d3a = min(d31, d32);
	d32 = max(d31, d32);
	d31 = min(d3a, d33); // Smallest now not in d32 or d33
	d33 = max(d3a, d33);
	d32 = min(d32, d33); // 2nd smallest now not in d33
	vec3 da = min(d11, d21);
	d21 = max(d11, d21);
	d11 = min(da, d31); // Smallest now in d11
	d31 = max(da, d31); // 2nd smallest now not in d31
	d11.xy = (d11.x < d11.y) ? d11.xy : d11.yx;
	d11.xz = (d11.x < d11.z) ? d11.xz : d11.zx; // d11.x now smallest
	d12 = min(d12, d21); // 2nd smallest now not in d21
	d12 = min(d12, d22); // nor in d22
	d12 = min(d12, d31); // nor in d31
	d12 = min(d12, d32); // nor in d32
	d11.yz = min(d11.yz,d12.xy); // nor in d12.yz
	d11.y = min(d11.y,d12.z); // Only two more to go
	d11.y = min(d11.y,d11.z); // Done! (Phew!)
	return sqrt(d11.xy); // F1, F2

}

// WORLEY NOISE





// vec2 worley(vec3 P, float jitter, bool manhattanDistance)
// float snoise(vec3 p)

float GetHeightAt(vec3 initialPos)
{
	float result = 0;
	
	{ // base noise
		vec3 pos = initialPos;
		int octaves = 2;
		float freq = 10;
		float ampModifier = 0.05;
		float freqModifier = 15;
		float amp = 1;
		pos *= freq;
		for (int i = 0; i < octaves; i++)
		{
			result += snoise(pos) * amp;
			pos *= freqModifier;
			amp *= ampModifier;
		}
	}
	

	{ // hill tops
		float p = snoise(initialPos * 10);
		if(p > 0) result -= p * 2;
	}
	
	{ // craters

		vec2 w = worley(initialPos*3, 1, false);
		result += smoothstep(0.0, 0.4, w.x) * 10;
	}

	return result;

}




uniform float planetRadiusMax;
uniform float planetRadiusVariation;
uniform vec3 offsetFromPlanetCenter;
uniform int numberOfVerticesOnEdge;
uniform vec3 cornerPositionA;
uniform vec3 cornerPositionB;
uniform vec3 cornerPositionC;
uniform int indiciesCount;

struct vec3_struct
{
    float x;
    float y;
    float z;
};
struct vec2_struct
{
    float x;
    float y;
    float z;
};

layout( binding=0 ) buffer buffer1 {
    vec3_struct positions[];
};
layout( binding=1 ) buffer buffer2 {
    vec3_struct normals[];
};
layout( binding=2 ) buffer buffer3 {
    vec2_struct uvs[];
};
layout( binding=3 ) buffer buffer4 {
    int indicies[];
};


layout( local_size_x = 1, local_size_y = 1, local_size_z = 1 ) in;
 


vec3 fromStruct(vec3_struct p)
{
	return vec3(p.x, p.y, p.z);
}
vec3_struct toStruct(vec3 p)
{
	vec3_struct s;
	s.x = p.x;
	s.y = p.y;
	s.z = p.z;
	return s;
}



vec3 GetNormal(int a, int b, int c)
{
	vec3 _a = fromStruct(positions[a]);
	vec3 _b = fromStruct(positions[b]);
	vec3 _c = fromStruct(positions[c]);
	return normalize(cross(_b - _a, _c - _a));
}








void main() {
	
	int id = int(gl_GlobalInvocationID.x);
	int tempId = id;
	int ix = 0;
	int iy = 0;
		
	int numberOfVerticesOnLine = 1;
	while(tempId >= numberOfVerticesOnLine && numberOfVerticesOnLine < numberOfVerticesOnEdge)
	{
		tempId -= numberOfVerticesOnLine;
		numberOfVerticesOnLine++;
		iy++;
	}
	ix = tempId;

	float x = 0;
	float y = 0;
	if(numberOfVerticesOnLine > 1) x = float(ix) / (numberOfVerticesOnLine-1);
	if(numberOfVerticesOnEdge > 1) y = float(iy) / (numberOfVerticesOnEdge-1);

	vec3 p = 
		cornerPositionA + 
		(cornerPositionB - cornerPositionA) * y * (1-x) + 
		(cornerPositionC - cornerPositionA) * y * x;

	p = normalize(p);
	p = p * (planetRadiusMax + planetRadiusVariation * GetHeightAt(p));
	p -= offsetFromPlanetCenter;

	// DEBUG
	//vec3 p = fromStruct(positions[id]);
	//p.x += 0.01f;

	positions[id] = toStruct(vec3(p));
}

